buildscript {
	repositories {
		jcenter()
        maven {
			name = 'Fabric'
			url = 'https://maven.fabricmc.net/'
		}
	}
	dependencies {
		classpath "net.fabricmc:fabric-loom:0.2.7-SNAPSHOT"
	}
}

apply plugin: net.fabricmc.loom.LoomGradlePlugin
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'java-library'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
	mavenCentral()
	jcenter()
}

version = "4.0.26"
group = 'RebornCore'

def ENV = System.getenv()
def build_number = ENV.BUILD_NUMBER ?: "local"
version = "${version}+build.$build_number"

configurations {
	ctRuntime {}
}

dependencies {
	minecraft "com.mojang:minecraft:1.15"
	mappings "net.fabricmc:yarn:1.15+build.2:v2"
	modImplementation "net.fabricmc:fabric-loader:0.7.2+build.174"

	//Fabric api
	modImplementation "net.fabricmc.fabric-api:fabric-api:0.4.24+build.279-1.15"

	compileOnly "com.google.code.findbugs:jsr305:3.0.2"

	modApi 'teamreborn:energy:0.0.8'
	include 'teamreborn:energy:0.0.8'

	ctRuntime "CurseTools:CurseTools:+"
}

processResources {
	inputs.property "version", project.version

	from(sourceSets.main.resources.srcDirs) {
		include "fabric.mod.json"
		expand "version": project.version
	}

	from(sourceSets.main.resources.srcDirs) {
		exclude "fabric.mod.json"
	}
}


task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = "sources"
	from sourceSets.main.allSource
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			// add all the jars that should be included when publishing to maven
			artifact(remapJar) {
				builtBy remapJar
			}
			artifact(sourcesJar) {
				builtBy remapSourcesJar
			}
		}
	}

	repositories {
		maven {
			url "http://mavenupload.modmuss50.me/"
			if (project.hasProperty('mavenPass')) {
				credentials {
					username 'buildslave'
					password project.getProperty('mavenPass')
				}
			}
		}
	}
}

task curseTools {
	doLast {
		logger.lifecycle(":launching CurseTools")
		ant.setLifecycleLogLevel "INFO"
		ant.java(classname: 'me.modmuss50.cursetools.Main', classpath: configurations.ctRuntime.asPath, fork: true, spawn: false) {
			arg(value: remapJar.archivePath.getAbsolutePath())
		}
	}
}

task copyToLib(type: Copy) {
    into "$buildDir/output/lib"
    from configurations.runtime
}
